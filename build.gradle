plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
	mavenCentral()
}

bootJar.enabled = false // 실행 파일 False


subprojects { //모듈 공통 적용할 사항들
	group = 'xyz.sellyourunhappiness'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '17'

	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	repositories {
		mavenCentral()
	}

	configurations {
		compileOnly {
			//롬복 라이브러리를 어노테이션 프로세서로 등록하기 위한 어노테이션 프로세싱 기능을 활성화하는 것을 자동으로 구성 https://kdev.ing/lombok-annotation-processor/
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testAnnotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.projectlombok:lombok'
	}


	tasks.named('test') {
		useJUnitPlatform()
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

project(':sellyourunhappiness-api') {
	dependencies {
		implementation project(':sellyourunhappiness-core')
	}
}
